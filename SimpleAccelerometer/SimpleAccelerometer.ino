#include "Arduino_BMI270_BMM150.h"

#define MINIMUM_TILT 5  // Threshold for tilt detection in degrees
#define WAIT_TIME 500   // How often to run the code in milliseconds

float x, y, z;
int angleX = 0;
int angleY = 0;
unsigned long previousMillis = 0;

void setup() {
  Serial.begin(9600);
  while (!Serial);

  if (!IMU.begin()) {
    Serial.println("Failed to initialize IMU");
    while (1);
  }

  Serial.print("Accelerometer sample rate = ");
  Serial.print(IMU.accelerationSampleRate());
  Serial.println("Hz");
}

void loop() {
  if (IMU.accelerationAvailable() && millis() - previousMillis >= WAIT_TIME) {
    previousMillis = millis();
    IMU.readAcceleration(x, y, z);

    // calculate tilt angles in degrees
    angleX = atan2(x, sqrt(y * y + z * z)) * 180 / PI;
    angleY = atan2(y, sqrt(x * x + z * z)) * 180 / PI;

    // determine the tilting direction based on angleX and angleY
    if (angleX > MINIMUM_TILT) { // tilting up
      Serial.print("Tilting up ");
      Serial.print(angleX);
      Serial.println(" degrees");
    } else if (angleX < -MINIMUM_TILT) { // tilting down
      Serial.print("Tilting down ");
      Serial.print(-angleX);
      Serial.println(" degrees");
    }

    if (angleY > MINIMUM_TILT) { // tilting right
      Serial.print("Tilting right ");
      Serial.print(angleY); 
      Serial.println(" degrees");
    } else if (angleY < -MINIMUM_TILT) { // tilting left
      Serial.print("Tilting left ");
      Serial.print(-angleY);
      Serial.print(" degrees");
    }
  }
}
